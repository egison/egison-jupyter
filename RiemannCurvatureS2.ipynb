{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "declare symbol r, θ, φ: MathExpr\n\ndef x : Vector MathExpr := [| θ, φ |]\n\ndef g{_i_j} : Matrix MathExpr := [| [| r^2, 0 |], [| 0, r^2 * (sin θ)^2 |] |]_i_j\n\ndef g{~i~j} : Matrix MathExpr := [| [| 1 / r^2, 0 |], [| 0, 1 / (r^2 * (sin θ)^2) |] |]~i~j\n\ndef Γ_i{_j_k} : Tensor MathExpr := (1 / 2) * (∂/∂ g_i_k x~j + ∂/∂ g_i_j x~k - ∂/∂ g_j_k x~i)\n\ndef Γ~i_j_k : Tensor MathExpr := withSymbols [m]\n  g~i~m . Γ_m_j_k\n\ndef R~i_j_k_l : Tensor MathExpr := withSymbols [m]\n  ∂/∂ Γ~i_j_l x~k - ∂/∂ Γ~i_j_k x~l + Γ~m_j_l . Γ~i_m_k - Γ~m_j_k . Γ~i_m_l"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "$$\\begin{pmatrix} 0 & sin(θ)^2 \\\\ -1 & 0 \\\\ \\end{pmatrix}_{\\;\\#}^{\\#\\;}$$"
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "R~#_#_1_2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "$$\\begin{pmatrix} 0 & -sin(θ)^2 \\\\ 1 & 0 \\\\ \\end{pmatrix}_{\\;\\#}^{\\#\\;}$$"
      ]
     },
     "execution_count": 9,
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "R~#_#_2_1"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {},
   "outputs": [],
   "source": [
    "def Ric[_i_j] : Matrix MathExpr := withSymbols [m]\n  sum (contract R~m_i_m_j)\n\ndef scalarCurvature : MathExpr := withSymbols [i, j]\n  g~i~j . Ric_i_j"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "$$\\frac{2}{r^2}$$"
      ]
     },
     "execution_count": 12,
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "scalarCurvature"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Egison",
   "language": "egison",
   "name": "egison"
  },
  "language_info": {
   "codemirror_mode": "egison",
   "file_extension": ".egi",
   "mimetype": "text/plain",
   "name": "egison"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}