{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "declare symbol r, θ, φ: MathExpr\n",
    "\n",
    "def x : Vector MathExpr := [| θ, φ |]\n",
    "\n",
    "def X : Vector MathExpr := [| r * sin θ * cos φ, r * sin θ * sin φ, r * cos θ |]\n",
    "\n",
    "def e_i_j : Matrix MathExpr := ∂/∂ X_j x~i"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "$$\\begin{pmatrix} r cos(θ) cos(φ) & r cos(θ) sin(φ) & -r sin(θ) \\\\ -r sin(θ) sin(φ) & r sin(θ) cos(φ) & 0 \\\\ \\end{pmatrix}_{\\#}^{\\;}$$"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "e_#"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "def g_i_j : Matrix MathExpr := generateTensor (\\[a, b] -> V.* e_a e_b) [2, 2]\n",
    "\n",
    "def g~i~j : Matrix MathExpr := M.inverse g_#_#"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "$$(\\begin{pmatrix} r^2 & 0 \\\\ 0 & r^2 sin(θ)^2 \\\\ \\end{pmatrix}_{\\#\\#}^{\\;\\;}, \\begin{pmatrix} \\frac{1}{r^2} & 0 \\\\ 0 & \\frac{1}{r^2 sin(θ)^2} \\\\ \\end{pmatrix}_{\\;\\;}^{\\#\\#})$$"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "(g_#_#, g~#~#)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [],
   "source": [
    "def Γ_i_j_k : Tensor MathExpr := (1 / 2) * (∂/∂ g_i_k x~j + ∂/∂ g_i_j x~k - ∂/∂ g_j_k x~i)\n",
    "\n",
    "def Γ~i_j_k : Tensor MathExpr := withSymbols [m]\n",
    "  g~i~m . Γ_m_j_k"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "$$(\\begin{pmatrix} 0 & 0 \\\\ 0 & -r^2 sin(θ) cos(θ) \\\\ \\end{pmatrix}_{\\#\\#}^{\\;\\;}, \\begin{pmatrix} 0 & r^2 sin(θ) cos(θ) \\\\ r^2 sin(θ) cos(θ) & 0 \\\\ \\end{pmatrix}_{\\#\\#}^{\\;\\;})$$"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "(Γ_1_#_#, Γ_2_#_#)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [],
   "source": [
    "def R~i_j_k_l : Tensor MathExpr := withSymbols [m]\n",
    "  ∂/∂ Γ~i_j_l x~k - ∂/∂ Γ~i_j_k x~l + Γ~m_j_l . Γ~i_m_k - Γ~m_j_k . Γ~i_m_l"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "$$(\\begin{pmatrix} 0 & sin(θ)^2 \\\\ -sin(θ)^2 & 0 \\\\ \\end{pmatrix}_{\\#\\#}^{\\;\\;}, \\begin{pmatrix} 0 & -1 \\\\ 1 & 0 \\\\ \\end{pmatrix}_{\\#\\#}^{\\;\\;})$$"
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "(R~1_2_#_#, R~2_1_#_#)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {},
   "outputs": [],
   "source": [
    "def Ric_i_j : Matrix MathExpr := withSymbols [m]\n",
    "  sum (contract R~m_i_m_j)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "$$\\begin{pmatrix} 1 & 0 \\\\ 0 & sin(θ)^2 \\\\ \\end{pmatrix}_{\\#\\#}^{\\;\\;}$$"
      ]
     },
     "execution_count": 10,
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "Ric_#_#"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "metadata": {},
   "outputs": [],
   "source": [
    "def scalarCurvature : MathExpr := withSymbols [i, j]\n",
    "  g~i~j . Ric_i_j"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "$$\\frac{2}{r^2}$$"
      ]
     },
     "execution_count": 12,
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "scalarCurvature"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Egison",
   "language": "egison",
   "name": "egison"
  },
  "language_info": {
   "codemirror_mode": "egison",
   "file_extension": ".egi",
   "mimetype": "text/x-egison",
   "name": "egison"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
