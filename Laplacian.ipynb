{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "declare symbol r, θ: MathExpr\n\ndef N : Integer := 2\n\ndef x : Vector MathExpr := [|r, θ|]\n\ndef g_i_j : Matrix MathExpr := [| [| 1, 0 |], [| 0, r^2 |] |]_i_j\n\ndef g~i~j : Matrix MathExpr := [| [| 1, 0 |], [| 0, 1 / r^2 |] |]~i~j\n\ndef d (A: Tensor MathExpr) : Tensor MathExpr := !(flip ∂/∂) x A\n\ndef hodge (A: Tensor MathExpr) : Tensor MathExpr :=\n  let k := dfOrder A in\n    withSymbols [i, j]\n      (sqrt (M.det g_#_#)) * (foldl (.) ((ε' N k)_(i_1)..._(i_N) . A..._(j_1)..._(j_k))\n                                        (map 1#g~(i_$1)~(j_$1) [1..k]))\n\ndef δ (A: Tensor MathExpr) : Tensor MathExpr :=\n  let k := dfOrder A in\n    -1^(N * (k + 1) + 1) * (hodge (d (hodge A)))\n\ndef Δ (A: Tensor MathExpr) : Tensor MathExpr :=\n  match (dfOrder A) as integer with\n  | #0 -> δ (d A)\n  | #N -> d (δ A)\n  | _  -> d (δ A) + δ (d A)\n\ndef f : MathExpr := function (r, θ)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "$$\\frac{-\\frac{\\partial^2 f}{\\partial θ^2} - r \\frac{\\partial f}{\\partial r} - r^2 \\frac{\\partial^2 f}{\\partial r^2}}{r^2}$$"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "Δ f"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Egison",
   "language": "egison",
   "name": "egison"
  },
  "language_info": {
   "codemirror_mode": "egison",
   "file_extension": ".egi",
   "mimetype": "text/plain",
   "name": "egison"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}